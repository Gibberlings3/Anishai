BACKUP  ~anishai/backup~
SUPPORT ~https://www.gibberlings3.net/forums/forum/28-miscellaneous-released-mods/~
VERSION ~v3~
README ~anishai/readme_anishai_%LANGUAGE%.txt~ ~anishai/readme_anishai_english.txt~

AUTO_TRA ~anishai/languages/%s~

ALWAYS

  ACTION_IF NOT VARIABLE_IS_SET bg2_chapter THEN BEGIN // check to make this happen only once per install

    ACTION_IF GAME_IS ~eet~ BEGIN
      OUTER_SET bg2_chapter = 12
    END ELSE BEGIN
      OUTER_SET bg2_chapter = 0
    END
    OUTER_FOR (i=1; i<=10; i=i+1) BEGIN
      OUTER_SET bg2_chapter = bg2_chapter + 1
      OUTER_SPRINT name_source ~bg2_chapter_%i%~
      OUTER_SET EVAL ~%name_source%~ = bg2_chapter
    END

    // convert strings to UTF-8 for BGEE/BG2EE
    ACTION_DEFINE_ARRAY cdreload BEGIN setup END
    ACTION_DEFINE_ARRAY cdnoconvert BEGIN ee END // List of tra files that contain ONLY strings for the WeiDU installer and NOT game content
    LAF HANDLE_CHARSETS INT_VAR infer_charsets = 1 STR_VAR tra_path = ~anishai/languages~ noconvert_array = cdnoconvert reload_array = cdreload END

  END

END

LANGUAGE ~English~
         ~english~
         ~anishai/languages/english/Setup.tra~

LANGUAGE ~Deutsch (Uebersetzung von Yago)~
         ~german~
         ~anishai/languages/english/Setup.tra~
         ~anishai/languages/german/Setup.tra~

LANGUAGE ~Italian (translated by Ilot)~
         ~italian~
         ~anishai/languages/english/Setup.tra~
         ~anishai/languages/italian/Setup.tra~

LANGUAGE ~Francais (traduit par Galathee)~
         ~french~
         ~anishai/languages/english/Setup.tra~
         ~anishai/languages/french/Setup.tra~

LANGUAGE ~Russian (prowler)~
         ~russian~
         ~anishai/languages/english/Setup.tra~
         ~anishai/languages/russian/Setup.tra~

BEGIN @0

ACTION_IF GAME_IS ~bg2ee eet~ BEGIN
  LOAD_TRA ~anishai/languages/%LANGUAGE%/ee.tra~
END

// The following is the copying of Anishai's .CRE file. It doesn't overwrite the original one.

COPY ~anishai/creatures/anishai.cre~ ~override~
  SAY NAME1 @1
  SAY NAME2 @1
  SAY BIO @2

COPY_EXISTING ~thief7.cre~ ~override~
  WRITE_ASCII 0x248 ~thief7~ #8
  WRITE_BYTE  0x27b 50 // chaotic neutral

COPY ~anishai/items/anidag.itm~ ~override~ // anishai's dagger
  SAY NAME1 @3
  SAY NAME2 @3
  SAY DESC  @4
  PATCH_IF GAME_IS ~bg2ee eet~ BEGIN
    WRITE_LONG 0x1e (BIT7 + BIT8 + BIT9 + BIT10 + BIT14 + BIT15) // normal dagger unusability since we're 319'ing it
    WRITE_BYTE 0x29 0
    WRITE_BYTE 0x2b BIT1
    WRITE_BYTE 0x2d 0
    WRITE_BYTE 0x2f 0
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 power = 1 parameter2 = 11 timing = 2 special = RESOLVE_STR_REF (@1) STR_VAR resource = anishai END
  END

INCLUDE ~anishai/lib/cd_portrait_copy.tph~
LAF cd_portrait_npc STR_VAR source = ~anishai~ source_path = ~anishai/portraits~ END

COMPILE ~anishai/scripts/thief7.baf~
COMPILE ~anishai/scripts/anishai.baf~

// The following is telling WeiDU to compile the following .d files

COMPILE ~anishai/dialogue/anishai.d~ EVALUATE_BUFFER
COMPILE ~anishai/dialogue/anishaiP.d~ EVALUATE_BUFFER

// The following is some .2DA appending for Anishai

APPEND ~pdialog.2da~ ~Anishai AnishaiP AnishaiJ AnishaiD~ UNLESS ~Anishai~ UNLESS ~25POST~

APPEND ~pdialog.2da~ ~Anishai AnishaiP AnishaiJ AnishaiD AnishaiP AnishaiJ AnishaiD Anishai25~ UNLESS ~Anishai~ IF ~25POST~

